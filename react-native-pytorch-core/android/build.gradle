/*
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["PyTorchCore_kotlinVersion"]

  if (project == rootProject) {
    repositories {
      google()
      jcenter()
    }
    dependencies {
      classpath "com.android.tools.build:gradle:3.5.3"
      // noinspection DifferentKotlinGradleVersion
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
  }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet('PyTorchCore_compileSdkVersion', 29)
  buildToolsVersion safeExtGet('PyTorchCore_buildToolsVersion', '29.0.2')
  defaultConfig {
    minSdkVersion safeExtGet('PyTorchCore_minSdkVersion', 28)
    targetSdkVersion safeExtGet('PyTorchCore_targetSdkVersion', 29)
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    packagingOptions {
      pickFirst '**/*.so'
    }
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
   disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    jcenter()

  // Repository holding the PyTorch Mobile libraries for Android
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
  }
}

def kotlin_version = safeExtGet('PyTorchCore_kotlinVersion', '1.3.50')
dependencies {
  implementation 'org.pytorch:pytorch_android_lite:1.10.0'
  implementation 'org.pytorch:pytorch_android_torchvision_lite:1.10.0'

  //noinspection GradleDynamicVersion
  api "com.facebook.react:react-native:+"  // From node_modules
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "androidx.appcompat:appcompat:1.2.0"
  implementation "com.android.support.constraint:constraint-layout:2.0.4"
  implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"

  implementation "androidx.cardview:cardview:1.0.0"
  implementation 'com.android.support:support-annotations:28.0.0'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.json:json:20140107'
  testImplementation 'org.mockito:mockito-core:1.10.19'

  androidTestImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test:core:1.3.0'
  androidTestImplementation 'androidx.test:runner:1.3.0'
  androidTestImplementation 'androidx.test:rules:1.3.0'
  androidTestImplementation 'com.facebook.soloader:soloader:0.9.0'

  def camerax_version = "1.1.0-alpha05"
  implementation "androidx.camera:camera-core:$camerax_version"
  implementation "androidx.camera:camera-camera2:$camerax_version"
  implementation "androidx.camera:camera-lifecycle:$camerax_version"
  implementation "androidx.camera:camera-view:1.0.0-alpha25"

  implementation "androidx.constraintlayout:constraintlayout:2.0.4"
}
